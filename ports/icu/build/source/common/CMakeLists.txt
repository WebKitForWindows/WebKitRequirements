# Copyright (c) 2018, NikitaFeodonit. All rights reserved.
#
## ICU build file for CMake build tools

set(lib_NAME ${ICULIBS_UC})
set(lib_NAME_SUFFIX ${COMMON_STUBNAME})
set(lib_OUTPUT_NAME ${lib_NAME}${ICULIBSUFFIX_DEBUG})

if(NOT WIN32)
  set(lib_RUNTIME_OUTPUT_NAME ${lib_OUTPUT_NAME})
else()
  set(lib_RUNTIME_OUTPUT_NAME ${lib_NAME}${PROJECT_VERSION_MAJOR}${ICULIBSUFFIX_DEBUG})
endif()

set(private_src_DIR "${CMAKE_CURRENT_LIST_DIR}")
set(interface_src_DIR "${includedir}")
set(build_src_DIR
  "$<BUILD_INTERFACE:${private_src_DIR}>"
)
set(install_src_DIR
  "$<INSTALL_INTERFACE:${interface_src_DIR}>"
)
set(public_src_DIR "${build_src_DIR}${install_src_DIR}")

add_library(${lib_NAME} "")

set_target_properties(${lib_NAME} PROPERTIES
  EXPORT_NAME ${lib_NAME_SUFFIX}
  OUTPUT_NAME ${lib_OUTPUT_NAME}
  RUNTIME_OUTPUT_NAME ${lib_RUNTIME_OUTPUT_NAME}
)

### Common libraries options
include(${PROJECT_SOURCE_DIR}/common_icu_lib_flags.cmake)
include(${PROJECT_SOURCE_DIR}/common_icu_lib_includes.cmake)

### Library's specific flags
# PRIVATE flags
set_property(TARGET ${lib_NAME} APPEND PROPERTY
  COMPILE_DEFINITIONS ${CPPFLAGSICUUC}
    U_COMMON_IMPLEMENTATION
    # for plugin configuration
    DEFAULT_ICU_PLUGINS=\"${libdir}/icu\"
)
if(PKGDATA_MODE STREQUAL "common")
  set_property(TARGET ${lib_NAME} APPEND PROPERTY
    COMPILE_DEFINITIONS
      # for icu data location
      U_ICU_DATA_DEFAULT_DIR=\"${ICUDATA_DIR}\"
  )
endif()
if(MSVC)
  set_property(TARGET ${lib_NAME} APPEND PROPERTY
    # Added in ICU 61.1.
    COMPILE_DEFINITIONS U_PLATFORM_USES_ONLY_WIN32_API=1
  )
  set_property(TARGET ${lib_NAME} APPEND PROPERTY
    COMPILE_DEFINITIONS $<$<CONFIG:Debug>:RBBI_DEBUG>
  )
endif()
set_property(TARGET ${lib_NAME} APPEND_STRING PROPERTY
  LINK_FLAGS ${LDFLAGSICUUC}
)

### Include directories
# PRIVATE
target_include_directories(${lib_NAME} PRIVATE
  ${private_src_DIR}
)

### Link libraries
# PUBLIC
if(PKGDATA_MODE STREQUAL "common")
  target_link_libraries(${lib_NAME} PUBLIC ${ICULIBS_STUBDT})
else()
  # INSTALL_INTERFACE introduced to avoid cyclic dependency:
  # * icupkg -> icuuc -> icudata -> icupkg (generated by)
  # Build:
  # * icudata -> icupkg -> icuuc -> icustubdata
  # Usage:
  # * icuuc -> icudata
  target_link_libraries(${lib_NAME} PUBLIC
    $<BUILD_INTERFACE:${ICULIBS_STUBDT}>
    $<INSTALL_INTERFACE:${ICULIBS_DT}>
  )
endif()
# After ICULIBS_DT or ICULIBS_STUBDT
target_link_libraries(${lib_NAME} PUBLIC ${DEFAULT_LIBS})

include(${CMAKE_CURRENT_LIST_DIR}/ICU-${PROJECT_VERSION}-source_files.cmake)

install(
  DIRECTORY ${CMAKE_CURRENT_LIST_DIR}/unicode
  DESTINATION "${includedir}"
  FILES_MATCHING
  PATTERN "*.h"
)

install(
  TARGETS ${lib_NAME}
  EXPORT "${TARGETS_EXPORT_NAME}"
  ARCHIVE  DESTINATION "${libdir}"
  LIBRARY  DESTINATION "${libdir}"
  RUNTIME  DESTINATION "${bindir}"
  INCLUDES DESTINATION "${includedir}"
)
